const std = @import("std");

const zx = @import("zx");

const app = @import("app");
const database = @import("database");

const auth = @import("../proxy.zx");

pub const options: zx.PageOptions = .{
    .methods = &.{ .GET, .POST },
};

fn handleForm(ctx: app.PageCtx) !zx.Component {
    const data = ctx.request.formData();
    const action = data.get("action") orelse return error.MissingAction;

    var session = try ctx.app.pool.getSession(ctx.arena);
    defer session.deinit();

    const is_register = std.mem.eql(u8, action, "register");
    const is_login = std.mem.eql(u8, action, "login");

    if (is_register or is_login) {
        const username = data.get("username") orelse return error.MissingUsername;
        const password = data.get("password") orelse return error.MissingPassword;

        const function: *const fn (*database.Session, database.User.AuthArgs) anyerror!database.User.AuthResponse = if (is_register)
            database.User.register
        else
            database.User.login;

        const response = try function(&session, .{
            .username = username,
            .password = password,
        });

        auth.setToken(&ctx.response, response.token);
    } else if (std.mem.eql(u8, action, "logout")) {
        const user_token = data.get("user_token") orelse return error.MissingUserToken;

        try database.User.logout(&session, user_token);

        auth.rmToken(&ctx.response);
    } else {
        return error.InvalidAction;
    }

    ctx.response.redirect("/", 302);

    return .none;
}

pub fn Page(ctx: app.PageCtx) !zx.Component {
    if (ctx.request.method == .POST) {
        return handleForm(ctx);
    }

    // TODO: send current window location in form, to redirect back to same page
    return (
        <div @allocator={ctx.arena} class="bg-secondary text-primary rounded-lg shadow-xl p-6 min-w-75 max-w-[95vw]">
            {if (ctx.state.token) |token| (
                <div>
                    <form method="post">
                        <input type="hidden" name="user_token" value={token} />

                        <input type="submit" name="action" value="logout" />
                    </form>
                </div>
            ) else (
                <div>
                    <form method="post">
                        <fieldset>
                            <legend>Username</legend>
                            <input id="username" name="username" type="text" required />
                        </fieldset>

                        <fieldset>
                            <legend>Password</legend>
                            <input id="password" name="password" type="password" required />
                        </fieldset>

                        <input type="submit" name="action" value="login" />
                        <input type="submit" name="action" value="register" />
                    </form>
                </div>
            )}
        </div>
    );
}
