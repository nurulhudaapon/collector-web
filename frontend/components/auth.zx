const std = @import("std");

const zx = @import("zx");
const js = zx.Client.js;

const api = @import("api");

const utils = @import("../utils.zig");

const dialog_id = "auth-dialog";
const submit_id = "submit-button";

const Action = enum {
    signin,
    login,
};

const UserData = struct {
    username: []const u8,
    token: []const u8,
};

var action: ?Action = null;
var user_data: ?UserData = null;

fn showForm() void {
    const allocator = std.heap.wasm_allocator;

    const a = action orelse @panic("action is null");

    if (utils.html.getElementById(allocator, dialog_id)) |dialog| {
        dialog.ref.call(void, "showModal", .{}) catch return;
    }

    if (utils.html.getElementById(allocator, submit_id)) |submit| {
        const text = switch (a) {
            .signin => "Sign in",
            .login => "Log in",
        };
        submit.ref.set("innerText", js.string(text)) catch return;
    }
}

fn openSignin(_: zx.EventContext) void {
    action = .signin;
    showForm();
}

fn openLogin(_: zx.EventContext) void {
    action = .login;
    showForm();
}

fn authenticate(_: zx.EventContext) void {
    const allocator = std.heap.wasm_allocator;

    const location = js.global.get(js.Object, "location") catch return;
    const referrer = location.getAlloc(js.String, allocator, "pathname") catch return;

    const username_input = utils.html.getElementById(allocator, "username") orelse return;
    const password_input = utils.html.getElementById(allocator, "password") orelse return;

    const username = username_input.ref.getAlloc(js.String, allocator, "value") catch return;
    const password = password_input.ref.getAlloc(js.String, allocator, "value") catch return;

    const response = switch (action orelse @panic("action is null")) {
        .signin => utils.api.execute(
            api.signin,
            allocator,
            "/api/signin",
            .{
                .username = username,
                .password = password,
                .referrer = referrer,
            },
        ) catch return,
        .login => utils.api.execute(
            api.login,
            allocator,
            "/api/login",
            .{
                .username = username,
                .password = password,
                .referrer = referrer,
            },
        ) catch return,
    };

    user_data = .{
        .username = username,
        .token = response.token,
    };
}

fn logout(_: zx.EventContext) void {
    const allocator = std.heap.wasm_allocator;

    const token = if (user_data) |data| data.token else @panic("user_data is null");
    const response = utils.api.execute(
        api.logout,
        allocator,
        "/api/logout",
        .{
            .token = token,
        },
    ) catch return;

    if (response.ok == 1) {
        user_data = null;
    }

    // TODO: redirect
}

fn closeForm(_: zx.EventContext) void {
    action = null;

    const allocator = std.heap.wasm_allocator;

    if (utils.html.getElementById(allocator, dialog_id)) |dialog| {
        dialog.ref.call(void, "close", .{}) catch {};
    }

    if (utils.html.getElementById(allocator, "username")) |username_input| {
        username_input.ref.set("value", js.string("")) catch {};
    }

    if (utils.html.getElementById(allocator, "password")) |password_input| {
        password_input.ref.set("value", js.string("")) catch {};
    }
}

pub fn Buttons(allocator: zx.Allocator) zx.Component {
    if (!utils.inClient()) {
        return (<></>);
    }

    const class = "text-primary bg-secondary rounded-md";

    return (
        <div @{allocator}>
            {if (user_data) |data| (
                <>
                    Welcome back, {data.username}
                    <button {class} onclick={logout}>Log out</button>
                </>
            ) else (
                <>
                    <button class=`{class} mr-2` onclick={openLogin}>Log in</button>
                    <button {class} onclick={openSignin}>Sign in</button>
                </>
            )}
        </div>
    );
}

pub fn Form(allocator: zx.Allocator) zx.Component {
    if (!utils.inClient()) {
        return (<></>);
    }

    return (
        <dialog @{allocator} id={dialog_id} class="backdrop:bg-black/50 rounded-lg ">
            <div class="bg-secondary text-primary rounded-lg shadow-xl p-6 min-w-75 max-w-[95vw]">
                <fieldset>
                    <legend>Username</legend>
                    <input id="username" name="username" type="text" required />
                </fieldset>

                <fieldset>
                    <legend>Password</legend>
                    <input id="password" name="password" type="password" required />
                </fieldset>

                <div class="flex gap-2 mt-4">
                    <button id={submit_id} type="button" onclick={authenticate} class="bg-accent3 rounded-3xl px-4 py-2" />

                    <button type="button" class="bg-gray-500 rounded-3xl px-4 py-2" onclick={closeForm}>
                        Cancel
                    </button>
                </div>
            </div>
        </dialog>
    );
}
