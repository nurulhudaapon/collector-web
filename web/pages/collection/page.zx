const std = @import("std");

const zx = @import("zx");
const js = zx.Client.js;

const api = @import("api");
const app = @import("app");
const database = @import("database");

const wasm = @import("../../wasm.zig");

// const text = await response.text()
fn onText(text: js.Object) !void {
    const slice = try text.value.string(wasm.allocator);

    const parsed: std.json.Parsed(api.Tracked) = try std.json.parseFromSlice(
        api.Tracked,
        wasm.allocator,
        slice,
        .{},
    );
    defer parsed.deinit();

    const element: js.Object = .{
        .value = @enumFromInt(parsed.value.js_ref),
    };

    try element.set("className", js.string(styleFor(parsed.value.tracked)));
    try element.call(
        void,
        "setAttribute",
        .{
            js.string("base_url"),
            js.string(urlFor(parsed.value.tracked)),
        },
    );
}

// const response = await fetch("/api")
fn onResponse(response: js.Object) !void {
    try wasm.text(response, onText);
}

fn toggleTrackingInner(ctx: zx.EventContext) !void {
    const target = ctx.getEvent().getTarget() orelse return;

    const base_url = try target.callAlloc(
        js.String,
        wasm.allocator,
        "getAttribute",
        .{js.string("base_url")},
    );
    defer wasm.allocator.free(base_url);

    const pokedex = try target.callAlloc(
        js.String,
        wasm.allocator,
        "getAttribute",
        .{js.string("pokedex")},
    );
    defer wasm.allocator.free(pokedex);

    const url = try std.fmt.allocPrint(
        wasm.allocator,
        "{s}?pokedex={s}&js_ref={}",
        .{ base_url, pokedex, @intFromEnum(target.value) },
    );
    defer wasm.allocator.free(url);

    try wasm.fetch(url, onResponse);
}

fn toggleTracking(ctx: zx.EventContext) void {
    if (zx.platform != .browser) return;
    toggleTrackingInner(ctx) catch {};
}

const ButtonProps = struct {
    is_tracked: bool,
    pokedex: api.database.Id,
    text: []const u8,
};

fn urlFor(is_tracked: bool) []const u8 {
    // NOTE: `/` in URL will be added in onClick's handler
    return if (is_tracked)
        "/api/untrack"
    else
        "/api/track";
}

fn styleFor(is_tracked: bool) []const u8 {
    return if (is_tracked)
        "px-3 py-1 rounded border border-green-600 text-white bg-green-600 hover:bg-green-700 font-semibold"
    else
        "px-3 py-1 rounded border border-gray-300 text-gray-900 bg-white hover:bg-gray-100 font-medium";
}

pub fn Button(allocator: zx.Allocator, props: ButtonProps) !zx.Component {
    const title = if (props.is_tracked) "Click to untrack" else "Click to track";
    const pressed = if (props.is_tracked) "true" else "false";
    return (
        <button @{allocator} onclick={toggleTracking} class={styleFor(props.is_tracked)} base_url={urlFor(props.is_tracked)} pokedex={props.pokedex} aria-pressed={pressed} title={title}>
            {props.text}
        </button>
    );
}

pub fn Buttons(
    allocator: zx.Allocator,
    props: struct { buttons: []const ButtonProps },
) !zx.Component {
    return (
        <div @{allocator} class="grid grid-cols-3 gap-2">
            {for (props.buttons) |button| (
                <Button {..button} />
            )}
        </div>
    );
}

pub fn Page(ctx: app.PageCtx) !zx.Component {
    var session = try ctx.app.pool.getSession(ctx.arena);
    defer session.deinit();

    const user = try ctx.state.getUser(&session) orelse {
        return (
            <>
                Must be logged in to access this page
            </>
        );
    };

    const all_species = try session
        .query(database.Species)
        .orderBy(.pokedex, .asc)
        .findAll();

    const buttons = try ctx.arena.alloc(ButtonProps, all_species.len);
    for (all_species, 0..) |species, i| {
        buttons[i] = .{
            .is_tracked = try user.isTracking(&session, species),
            .pokedex = species.pokedex,
            .text = species.name,
        };
    }

    return (
        <div @allocator={ctx.allocator} class="flex flex-col">
            <h1 class="text-2xl font-semibold mb-2">Your Collection</h1>
            <p class="text-sm text-gray-600 mb-4">Manage which species you track â€” click a card to toggle tracking.</p>

            <a href="/progress" class="text-sm text-blue-600 hover:underline mb-4">
                See your progress
            </a>

            <Buttons @rendering={.client} {buttons} />
        </div>
    );
}
